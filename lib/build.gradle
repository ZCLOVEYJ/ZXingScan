apply plugin: 'com.android.library'
apply plugin: 'maven'
apply from: 'config.gradle'

android {

    compileSdkVersion project.ext.android["compileSdkVersion"]

    defaultConfig {
        minSdkVersion project.ext.android["minSdkVersion"]
        targetSdkVersion project.ext.android["targetSdkVersion"]
        versionCode project.ext.android["versionCode"]
        versionName project.ext.android["versionName"]
        resourcePrefix "zxing_"
        consumerProguardFiles 'proguard-rules.pro'
    }


    lintOptions {
        abortOnError false
        warning 'InvalidPackage'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}


dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    implementation "com.android.support:appcompat-v7:28.0.0"
    implementation "com.google.zxing:core:3.4.0"
}

uploadArchives {
    configuration = configurations.archives

    Properties properties = new Properties()
    properties.load(project.file('maven.properties').newInputStream())
    def userName = properties.getProperty('maven.user')
    def password = properties.getProperty('maven.password')
    def mavenUrl = properties.getProperty('maven.url')
    def versionName = project.ext.android["versionName"]
    if (project.ext.android["debugVersion"]) {
        mavenUrl = properties.getProperty('maven.debugurl')
        versionName = versionName + "-SNAPSHOT"
    }

    repositories {
        mavenDeployer {
            snapshotRepository(url: mavenUrl) {
                authentication(userName: userName, password: password)
            }

            repository(url: mavenUrl) {
                authentication(userName: userName, password: password)
            }

            pom.project {
                groupId 'com.relxtech.android'
                artifactId 'zxing'
                version versionName
                packaging 'aar'
            }
        }
    }
}
